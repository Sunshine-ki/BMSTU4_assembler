; Особенность всех цепочечных команд в том, что они,
; кроме обработки текущего элемента цепочки, осуществляют еще и
; автоматическое продвижение к следующему элементу данной цепочки.
StkSeg  SEGMENT PARA STACK 'STACK'
        DB      200h DUP (?)
StkSeg  ENDS

DataS   SEGMENT WORD 'DATA'
my_string    DB   '123456' ;10  
                DB   '$'               
DataS   ENDS

Code    SEGMENT WORD 'CODE'
    ASSUME  cs:Code, es:DataS
main:
    ; Наша строка должна находится по адресу es:di.
    mov ax, DataS                 
    mov es, ax 
    mov di, OFFSET my_string
    ; В al должен находится символ, с которым мы будем срванивать.
    mov al, '$'
    ; CLD опускает флаг DF (Т.е. делает его равны 0).
    ; Это нужно для работы scasb.
    ; Данная команта опирается на флаг DF и, если, он равен нулю, 
    ; То мы будем двигаться вперед по строке (Т.е. к di будет прибавляться 1)
    ; Если же DF будет равен 1, то от di будет отниматься 1. 
    ; Т.е. мы будем двигаться назад по строке.
    CLD
    ; Далее описано для чего это.
    mov cx, 0FFFFh ; Т.к. начинаем с буквы вначале пишем 0.
    ; repne будет повторять команду (которая стоит после repne),
    ; пока элементы (al и es:di) не равны, (т.е. до первого равенства) 
    ; В любом случае выполняется не более СХ операций над последовательными элементами.
    ; zf != 1 or cx != 0
    ; Поэтому CX может служить нам как счетчик.
    ; Сначала мы в CX помещаем так называемую "максимальную длину строки"
    ; Затем, после выполнения команды scasb наш счетчик CX будет уменьшаться на единицу. 
    ; В итоге либо cx обнулится (И repne завершит повторение команды),
    ; Либо мы найдем совпадающие символы (Повторение тоже в этом случае завершится)
    ; И тогда мы будем знать, что в cx уменьшился на количество выполненых повторений.
    repne scasb
    ; И чтобы узнать длину строки нам нужно из "максимальной длины строки" вычесть cx
    mov ax, 0FFFFh
    sub ax, cx
    dec ax ; Чтобы не считать терминирующий символ.

    ; scasb в свою очередь:
    ; Вычитает из al es:di (al и es:di не изменяются).
    ; Поднимает флаги
    ; Либо увеличивает, либо уменьшает di (В зависимости от флага DF) 

    mov   AH,4Ch                  
    int   21h  
Code    ENDS
    END   main

